parserImport XOCL;

context OCL
  
  @Class Import extends OCL::OCL
    
    // An 'import' clause at the start of a compilation unit.
      
    @Attribute nameSpace : [String] end
      
    @Constructor(nameSpace) ! end
      
    @Operation desugar():Performable
      [| let nameSpace = <OCL::Path::toPath(nameSpace)> 
         in Kernel_import(nameSpace.contents);
            Kernel_letVar(nameSpace.name,nameSpace) 
         end 
      |]
    end

    @Operation lookup():Element
      nameSpace.lookup()
    end

    @Operation getNameSpace(nameSpaces:[NameSpace]):NameSpace
      @Find(n,nameSpaces) 
        when n.lookupPath(nameSpace,null) <> null 
        do n.lookupPath(nameSpace,null) 
        else null 
      end
    end
      
    @Operation pprint(out,indent)
      format(out,"import ~{::~;~S~}",Seq{nameSpace})
    end
      
  end
  